# Clone scanner script
# Written by HelLViS69 (2005)
# TODO:
# Languaje files
# Help files

addon.register("HellClonescan","1.2.8","HelL Clonescan",$tr("A simple script for scanning clones in channels",helltrix),"4.0.4",hell_clonescan.png)
{
	# This is our uninstall callback: it will be called by KVIrc when addon.uninstall is invoked
	popup.delitem -d -q channeltextview 696969
	event(OnJoin,clonescan){}
	alias(hell_clonescan){}

	objects.killclass "helltrixclone"

	trunload "helltrix"

	foreach(%win,%HClone_win{}){
		window.close -q %win;
	}
	%HClone_win{}=$nothing;

	# Done
	#echo -i=116 Bye 
}

# Ok, addon.register succeeded. We can go on with the installation.

# Get the path that this script was launched from
%mypath = $file.extractPath($0)

# copy files in each subdirectory
# the pics
if($file.exists(%mypath"/pics/"))
	addon.installfiles -q -s "HellClonescan" "pics" %mypath"/pics/*.png"

# the translations
if($file.exists(%mypath"/locale/"))
	addon.installfiles -q -s "HellClonescan" "locale" %mypath"/locale/*.mo"

# the documentation
if($file.exists(%mypath"/help/en/hellclonescan/"))
	addon.installfiles -q -s "HellClonescan" "help/en/hellclonescan/" %mypath"/help/en/hellclonescan/*.html"

# script Class
class(helltrixclone,object){
	constructor(){
	}

	cloneCheck($0=nick,$1=host){
		%checkNick=$0
		%checkHost=$1

		if(!%checkNick){
			# Global scan
			%i=0
			# Channel mask array in the form nick!ident@host
			foreach(%mask,$chan.users(,,m)){
				$$->%DbNick[%i]=$mask.nick(%mask)
				$$->%DbHost[%i]=$mask.host(%mask)
				%i++;
			}
			%NumUsers=%i

			for(%i=0; %i< $(%NumUsers - 1); %i++){
				if(!$$->%DbCloneNum[%i]){
					for(%j=$(%i + 1); %j< %NumUsers; %j++){
						if($str.cmp($$->%DbHost[%i],$$->%DbHost[%j])==0){
							if(!$$->%DbCloneNum[%i]){
								$$->%DbCloneNick{%i}=$$->%DbNick[%i]", "$$->%DbNick[%j]
								$$->%DbCloneNum[%i] = 2
							} else {
								$$->%DbCloneNick{%i} .= ", "$$->%DbNick[%j]
								$$->%DbCloneNum[%i]++
							}
							# We mark the clone
							$$->%DbCloneNum[%j] = -1
						}
					}
				}
			}
		} else {
			# Nick scan
			if (!%checkHost){
				%checkHost=$hostname(%checkNick)
				if(!%checkHost){
					# Just for sanity check
					echo -w=$active $tr("[BUG]: The system could not retrieve the host of",helltrix) %checkNick
					return
				}
			}
			$$->%DbCloneNick{%checkNick} = $chan.users(,"*!*@"%checkHost,)
			if ($str.contains($$->%DbCloneNick{%checkNick}, ",")){
				$$->%DbCloneNick{%checkNick} = $str.replace($$->%DbCloneNick{%checkNick}, ",", ", ")
				$$->%DbHost = %checkHost
			} else {
				$$->%DbCloneNick{%checkNick} =
			}
		}
	}

	createWin(){
		# Environment creation
		if (!%HClone_win{$context} || !$window.exists(%HClone_win{$context})){
			%HClone_win{$context} = $window.open(,$tr("Clones",helltrix) $context.networkName,$context,113)
		} else {
			window.maximize %HClone_win{$context}
		}
		echo -w=%HClone_win{$context} -i=114 $b$tr("Scanning for clones in",helltrix) $chan.name
		return %HClone_win{$context}
	}

	output($0=windowId,$1=nick,$2=host){
		if($0){
			%win=$0
		} else {
			%win=$channel
		}
		%checkNick=$1
		
		if(!%checkNick){
			# Output for global scan
			%cf=0
			foreach(%i,$keys($$->%DbCloneNick)){
				echo -w=%win -i=117 "  "$b$$->%DbCloneNick{%i} $b$tr("have same host",helltrix) $b$$->%DbHost[%i]
				%cf++
			}
			if(!%cf)
				echo -w=%win -i=114 $b$tr("  No clones found on",helltrix) $chan.name
			echo -w=%win -i=114 $b$tr("Scan End",helltrix)
		} else {
			# Output for nick scan
			if($$->%DbCloneNick{%checkNick}){
				echo -w=%win -i=117 $b$$->%DbCloneNick{%checkNick} $b$tr("have same host",helltrix) $b$$->%DbHost
			} elseif (!$2) {
				echo -w=%win -i=114 $tr("No clones found for",helltrix) $b%checkNick
			}
		}
	}
}

# Script Alias
alias(hell_clonescan)
{
	if (!$channel){
		echo -w=$active -i=118 $tr("[Clonescan]: This is not a channel window",helltrix)
		return
	}
	%isNick=$0
	%clonescan=$new(helltrixclone)
	
	if(!%isNick){
		%clonescan->$cloneCheck()
		%win=%clonescan->$createWin()
		%clonescan->$output(%win,)
	} else {
		# If we want a nick scan
		%isHost=$1
		if (!$sw(j) && !$chan.ison(%isNick)){
			echo -w=$active -i=114 $b%isNick $b$tr("is not in",helltrix) $b$target
		} elseif (!%isHost || (%isHost == $hostName(%isNick)) || $sw(j)){
			if ($sw(j) && (%isHost != $hostName(%isNick))){
				# This only happens, in the case of a join and fast change to vhost
				%isHost=$hostName(%isNick)
			}
			%clonescan->$cloneCheck(%isNick,%isHost)
			%clonescan->$output(,%isNick,%isHost)
		} else {
			echo -w=$active $tr("Usage",helltrix): /hell_clonescan [nick]
		}
	}

	delete %clonescan
}

# Script Event
event(OnJoin,clonescan)
{
	hell_clonescan -j $0 $2
}

# Script Popups
defpopup-m ("channeltextview")
{
	popup(HelLViS69 Clonescan,113,696969)
	{
		item("Clonescan", )
		{
			hell_clonescan
		}
	}
}
